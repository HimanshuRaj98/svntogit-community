# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wireplumber
pkgver=0.3.95
pkgrel=1
pkgdesc="Session / policy manager implementation for PipeWire"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'cpptoml' 'glib2' 'gobject-introspection' 'hotdoc' 'lua53'
'meson' 'pipewire' 'systemd')
checkdepends=('pipewire-alsa' 'pipewire-jack' 'pipewire-pulse')
optdepends=('lua53: for LUA scripting')
provides=('libwireplumber-0.3.so')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/pipewire/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('39e8eddc7152a062414dc19b51a8bfa105b1446a2cc9fca17f1ea7b42f6333d3e34e1eb996dfbe1222475ad9ce7b58f0bac2969e1763885c59e7aea160cb58fc')
b2sums=('b80f6b6f1fd25ce9e058cdcf97433df1b91b6ee8135f137c610e6bc0e0dcba5dc409855f3feede390ca88ec117346e1c4d6210bb9180c83553ca47ff3d39d037')

build() {
  cd "$pkgname-$pkgver"
  arch-meson \
    -Dsystem-lua=true \
    -Dsystemd-system-service=true \
    -Dsystemd-user-service=true \
    build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test || echo "Known to fail: https://gitlab.freedesktop.org/pipewire/wireplumber/-/issues/18"
}

package() {
  depends+=('libgio-2.0.so' 'libglib-2.0.so' 'libgmodule-2.0.so'
  'libgobject-2.0.so' 'libpipewire-0.3.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {NEWS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
