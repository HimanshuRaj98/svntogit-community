# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=wireplumber
pkgname=('wireplumber' 'wireplumber-docs')
pkgver=0.3.96
pkgrel=1
pkgdesc="Session / policy manager implementation for PipeWire"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
makedepends=('cmake' 'cpptoml' 'doxygen' 'glib2' 'gobject-introspection'
'hotdoc' 'lua53' 'meson' 'pipewire' 'python-breathe' 'python-sphinx_rtd_theme'
'python-sphinx' 'systemd')
checkdepends=('pipewire-alsa' 'pipewire-jack' 'pipewire-pulse')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/pipewire/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6af0d04186601bfb1565eca0ece9c0b529757a86320c1f19b2bed4d5a349f41b960627692db2e70e2b543009bbbed3f31159199290193cd4cea983bc9f9cbd79')
b2sums=('68d91840c790d75fb5662a44a1936a19b29166e0445d666277627b14cb7559122e93b01c35ea489e5ea4d43b27424974ab21ad888e989900d6a9ee5704c9db87')

build() {
  cd "$pkgname-$pkgver"
  arch-meson \
    -Dsystem-lua=true \
    -Dsystemd-system-service=true \
    -Dsystemd-user-service=true \
    build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test || echo "Known to fail: https://gitlab.freedesktop.org/pipewire/wireplumber/-/issues/18"
}

package_wireplumber() {
  depends=('gcc-libs' 'glibc' 'lua53' 'libgio-2.0.so' 'libglib-2.0.so'
  'libgmodule-2.0.so' 'libgobject-2.0.so' 'libpipewire-0.3.so')
  provides=('libwireplumber-0.3.so')

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {NEWS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove files provided by wireplumber-docs
  rm -frv "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_wireplumber-docs() {
  pkgdesc+=" (documentation)"

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # remove files provided by wireplumber
  rm -frv "${pkgdir}/usr/"{bin,include,lib,share/{gir*,wireplumber}} "${pkgdir}/etc/"
}
